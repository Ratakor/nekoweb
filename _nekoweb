#compdef nekoweb
_nekoweb() {
    local state line
    _arguments -s \
        '1: :->cmd' \
        '*: :->args'

    case $state in
    cmd)
        main_commands=('info\:"Display informations about a Nekoweb website" create\:"Create a new file or directory" upload\:"Upload files to your Nekoweb website" delete\:"Delete file or directory from your Nekoweb website" move\:"Move/Rename a file or directory" list\:"List files from your Nekoweb website" logout\:"Remove your API key from the save file" help\:"Display information about a command" version\:"Display program version"')
        main_commands=($main_commands)
        _alternative "args:command:(($main_commands))"
        ;;
    args)
        case $line[1] in
        create)
            _arguments -s '2:pathname:' '-d[Create a directory]'
            ;;
        upload)
            _arguments -s '*:file:_files'
            ;;
        delete)
            local server_files
            IFS=$'\n' server_files=($(nekoweb list --no-color 2>/dev/null))
            _arguments -s "*:file:(($server_files))"
            ;;
        move)
            _arguments -s '2:oldpath:_files' '3:newpath:_files'
            ;;
        list)
            local server_directories
            IFS=$'\n' server_directories=($(nekoweb list --no-color --only-dir 2>/dev/null))
            _arguments -s \
                "2:directory:(($server_directories))" \
                "--no-color[Disable color output]" \
                "--only-dir[Display only directories]"
            ;;
        help)
            _arguments -s '2:commands:(info create upload delete move list logout help version)'
            ;;
        esac
        ;;
    esac
}
